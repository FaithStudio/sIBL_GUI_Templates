[Template]
Name = @Name | 3dsMax Mental Ray Standard | String | Template Name
Path = @Path | | String | Template Path
HelpFile = @HelpFile | help/3dsMax_MR_Standard_Template_Manual.html | String | Help File
Release = @Release | 2.0.4 | String | Template Release
Date = @Date | 27 November 2012 | String | Date
Author = @Author | Kel Solaar / Dschaga / Jeff Hanna / JHaywood  | String | Author
Email = @Email | thomas.mansencal@gmail.com | String | Email
Url = @Url | http://thomasmansencal.com/ | String | Url
Software = @Software | 3dsMax  | String | Software
Version = @Version | 2010 - 2014 | String | Version
Renderer = @Renderer | Mental Ray | String | Renderer
OutputScript = @OutputScript | sIBL_3dsmax_Import.ms | String | Output Script
Comment = @Comment | This is 3dsmax Mental Ray Template. | String | Comment

[Ibl Set Attributes]
Background|BGfile = @BGfile
Reflection|REFfile = @REFfile
Enviroment|EVfile = @EVfile
Sun|SUNu = @SUNu
Sun|SUNv = @SUNv
Sun|SUNcolor = @SUNcolor
Sun|SUNmulti = @SUNmulti
Lights|DynamicLights = @dynamicLights

[Common Attributes]
sceneSetupPrefix = @sceneSetupPrefix | sIBL | String | Scene Setup Prefix
createBackground = @createBackground | 1 | Boolean | Create Background
createReflection = @createReflection | 1 | Boolean | Create Reflection
createLighting = @createLighting | 1 | Boolean | Create Lighting
createSun = @createSun | 1 | Boolean | Create Sun
createLights = @createLights | 1 | Boolean | Create Dynamic Lights

[Additional Attributes]
displayFeedback = @displayFeedback | 1 | Boolean | Display Feedback
sunLightType = @sunLightType | mrSun;Direct;Spot;Omni;Photometric;mrAreaSpot;mrAreaOmni | Enum | Sun Light Type
dynamicLightsType = @dynamicLightsType | Direct;Spot;Omni;Photometric;mrSun;mrAreaSpot;mrAreaOmni | Enum | Dynamic Lights Type
hideLights = @hideLights | 1 | Boolean | Hide Lights
createGround = @createGround | 1 | Boolean | Create Ground
shadowCatcher = @shadowCatcher | 1 | Boolean | Ground Shadow Catcher
activateLinearWorkflow = @activateLinearWorkflow | 1 | Boolean | Activate Linear Workflow

[Remote Connection]
ConnectionType = @ConnectionType | Win32 | String
TargetApplication = @TargetApplication | MAX.Application | String
ExecutionCommand = @ExecutionCommand | fileIn ("$loaderScriptPath") | String

[Script]
/*
@OutputScript - @Release for @Software @Version
Author: @Author
EMail: @Email
Homepage: @Url
Template path: @Path
Template last modified: @Date
sIBL_GUI
*/

struct __constants (applicationName, prefix, title, defaultGamma, globalPreProcessCallback, globalPostProcessCallback, localPreProcessCommand, localPostProcessCommand)
struct __data (backgroundFilePath, reflectionFilePath, lightingFilePath, sunU, sunV, sunColor, sunMultiplier, dynamicLights)
struct __options (createBackground, createLighting, createReflection, createSun, createLights, displayFeedback, feedbackRadius, hideLights, sunLightType, dynamicLightsType, createGround, shadowCatcher, activateLinearWorkflow)
struct __sceneNodes (dataGroup, lightsGroup, feedback)

global Constants = __constants applicationName:"sIBL_GUI" prefix:"@sceneSetupPrefix" title:"Smart IBL" defaultGamma:2.2 globalPreProcessCallback:"sIBL_3dsmax_preProcessCallback" globalPostProcessCallback:"sIBL_3dsmax_postProcessCallback" localPreProcessCommand:"sIBL_3dsmax_MR_preProcessCallback" localPostProcessCommand:"sIBL_3dsmax_MR_postProcessCallback"
global Datas = __data backgroundFilePath:"@BGfile" reflectionFilePath:"@REFfile" lightingFilePath:"@EVfile" sunU:@SUNu sunV:@SUNv sunColor:"@SUNcolor" sunMultiplier:@SUNmulti dynamicLights:"@dynamicLights"
global UserOptions = __options createBackground:@createBackground createReflection:@createReflection createLighting:@createLighting createSun:@createSun createLights:@createLights displayFeedback:@displayFeedback feedbackRadius:1000 sunLightType:"@sunLightType" dynamicLightsType:"@dynamicLightsType" hideLights:@hideLights createGround:@createGround shadowCatcher:@shadowCatcher activateLinearWorkflow:@activateLinearWorkflow
global SceneNodes = __sceneNodes()

fn getSceneExtent =
(
	sceneExtent = 0
	for object_ in Geometry do
	(
		bBox = nodeGetBoundingBox object_ (Matrix3 1)
		for i = 1 to bBox.count  do
		(
			toArray = #(abs(bBox[i].x), abs(bBox[i].y), abs(bBox[i].z))
			if amax(toArray) > sceneExtent then sceneExtent = amax(toArray)
		)
	)

	return sceneExtent
)

fn truncFloatNumber number truncValue =
(
	if truncValue != 0 then
		return floor(number / truncValue) * truncValue
	else
		return number
)

fn getExtendedFeedbackRadius feedbackRadius =
(
	sceneExtent = getSceneExtent()
	sceneExtent = sceneExtent + (sceneExtent*50/100)

	if sceneExtent < feedbackRadius then
		return feedbackRadius
	else
		return truncFloatNumber sceneExtent 10
)

fn hasProductionShadersSupport =
(
	if(maxVersion())[1]/1000 < 13 then
		return False
	else
		return True
)

fn getDefaultControllers currentNode =
(
	currentNode.Transform.controller = prs ()
	currentNode.rotation.controller = Euler_XYZ ()
	currentNode.scale.controller = bezier_scale ()
)

fn environmentDeletion =
(
	-- Nodes Deletion.
	delete (execute ( "$" + Constants.prefix + "*"))
	
	-- Environment Map Deletion.
	environmentMap = undefined

	-- Lens Shader Deletion.
	if(classOf renderers.current == mental_ray_renderer) then
		renderers.current.Camera_Lens_Shader = undefined

	freeSceneBitmaps()
)

fn activateRenderer =
(
	if(IsKindOf renderers.current mental_ray_renderer == False ) then
	(
		renderers.current = mental_ray_renderer()

		renderers.current.MinimumSamples = 0
		renderers.current.MaximumSamples = 2

		if((maxVersion())[1]/1000 >= 11) then
			renderers.current.RestoreFinalGatherPreset 4
	)

	renderers.current.FinalGatherEnable2 = True
)

fn hideLights =
(
	hide lights
)

fn getControlsGroup =
(
	local controlsPoint = Point name:(Constants.prefix + "_Point") centermarker:off axistripod:off cross:off Box:off
	SceneNodes.dataGroup = group controlsPoint name:(Constants.prefix + "_Data")
)

fn getFeedback feedbackRadius createBackground texturePath =
(
	local feedbackSphere = Sphere radius:feedbackRadius segs:32 mapcoords:on backfacecull:on renderable:off

	getDefaultControllers feedbackSphere

	local normalModifier_ = Normalmodifier()
	normalModifier_.flip = True
	addModifier feedbackSphere normalModifier_
	collapseStack feedbackSphere

	if(createBackground == 1) then
	(
		local feedbackShader = StandardMaterial name:(Constants.prefix + "_FeedbackShader")
		feedbackShader.selfIllumAmount = 100
		feedbackShader.diffuse = color 255 255 255
		feedbackShader.ambient = feedbackShader.diffuse

		feedBackTexture = Bitmaptexture fileName:texturePath
		feedBackTexture.coordinates.u_tiling = -1
		feedbackShader.diffuseMap =  feedBackTexture
		feedbackShader.selfillumMap =  feedBackTexture

		feedbackSphere.material = feedbackShader
		showTextureMap feedbackShader True
	)

	feedbackSphere.parent = SceneNodes.dataGroup
	setTransformLockFlags feedbackSphere #{1,2,3,4,5}

	feedbackSphere.name = (Constants.prefix + "_Feedback")
	SceneNodes.feedback = feedbackSphere
)

fn getLightsGroup =
(
	local controlsPoint = Point name:(Constants.prefix + "_Lights_Point") centermarker:off axistripod:off cross:off Box:off
	SceneNodes.lightsGroup = group controlsPoint name:(Constants.prefix + "_Lights")

	getDefaultControllers SceneNodes.lightsGroup

	SceneNodes.lightsGroup.parent = SceneNodes.dataGroup
)

fn createSkyLight =
(
	local iblSkylight = Skylight()
	iblSkylight.sky_mode = 0

	iblSkylight.parent = SceneNodes.dataGroup

	iblSkylight.name = (Constants.prefix + "_SkyLight")
)

fn getLight lightType lightName lightColor intensity lightDistance uCoordinate vCoordinate =
(
	if(SceneNodes.lightsGroup == undefined) then
		getLightsGroup()

	uCoordinate += 0.75
	projectedRadius = cos((0.5 - vCoordinate) * 180)

	local xPos = lightDistance * ((cos(uCoordinate * 360)) * projectedRadius)
	local yPos = lightDistance * ((sin(uCoordinate * 360)) * projectedRadius) * -1
	local zPos = lightDistance * (sin((0.5 - vCoordinate) * 180))

	local lightTarget = TargetObject transform:(matrix3 [1,0,0] [0,1,0] [0,0,1] [0,0,0])
	lightTarget.parent = SceneNodes.lightsGroup

	lightColorTokens = filterstring lightColor ","
	local lightColor = color ((pow ((lightColorTokens[1] as float) / 255) Constants.defaultGamma) * 255) ((pow ((lightColorTokens[2] as float) / 255) Constants.defaultGamma) * 255) ((pow ((lightColorTokens[3] as float) / 255) Constants.defaultGamma) * 255)
	local lightFalloff = lightDistance + 5

	if(lightType == "Direct") then
	(
		local light = TargetDirectionalLight castShadows:on hotspot:lightDistance falloff:lightFalloff
	)
	else if(lightType == "Spot") then
	(
		local light = TargetSpot castShadows:on hotspot:lightDistance falloff:lightFalloff
	)
	else if(lightType == "Omni") then
	(
		local light = OmniLight castShadows:on
	)
	else if(lightType == "Photometric") then
	(
		local light = Target_Light rgbFilter:lightColor castShadows:on
		light.intensity = 1.0 * (intensity as float)  * (lightDistance * lightDistance)
	)
	else if(lightType == "mrAreaSpot") then
	(
		local light = miAreaLight()
		light.Rectangle_Height = 16
		light.Rectangle_Width = 16
		light.Disc_Radius = 16
	)
	else if(lightType == "mrAreaOmni") then
	(
		local light = miAreaLightOmni()
		light.Sphere_Radius = 16
		light.Cylinder_Height = 16
	)
	else if(lightType == "mrSun") then
	(
		local multiplier =  (intensity as float)*0.03
		local light = mr_Sun skymult:multiplier pos:[(xPos as float),  (yPos as float), (zPos as float)] target:lightTarget
	)

	if(lightType == "Direct" or lightType == "Spot" or lightType == "Omni" or lightType == "Photometric" or lightType == "mrAreaSpot" or lightType == "mrAreaOmni") then
	(
		light.rgb = lightColor
		light.multiplier = (intensity as float)
		if(lightType != "mrAreaSpot" and lightType != "mrAreaOmni") then
			light.shadowGenerator = RaytraceShadow()
	)

	light.pos = [(xPos as float),  (yPos as float), (zPos as float)]
	light.target = lightTarget

	light.parent = SceneNodes.lightsGroup

	lightTarget.name = lightName + "_Target"
	light.name = lightName
)

fn createSun sunLightType sunColor sunMultiplier lightDistance sunU sunV =
(
	getLight sunLightType (Constants.prefix + "_Sun") sunColor sunMultiplier lightDistance sunU sunV
)

fn createDynamicLights dynamicLightsType dynamicLights lightDistance =
(
	local dynamicLightsTokens = filterString dynamicLights "|"

	for i = 1 to dynamicLightsTokens.count by 8 do
	(
		local lightName = Constants.prefix + substituteString dynamicLightsTokens[i+1] " " "_"
		local lightColor = dynamicLightsTokens[i+2] + "," + dynamicLightsTokens[i+3] + "," + dynamicLightsTokens[i+4]
		local intensity = dynamicLightsTokens[i+5] as float
		local uCoordinate = dynamicLightsTokens[i+6] as float
		local vCoordinate = dynamicLightsTokens[i+7] as float

		getLight dynamicLightsType lightName lightColor intensity lightDistance uCoordinate vCoordinate
	)
)

fn getEnvironmentBranch branchName filePath =
(
	local texture = Bitmaptexture()
	texture.filename = filePath
	texture.coordinates.mappingtype = 1
	texture.coordinates.mapping = 0
	texture.coordinates.U_Tiling = -1.0

	local gammaGain =  Utility_Gamma___Gain__mi()
	gammaGain.gamma = 1.0
	gammaGain.input_shader = texture

	texture.name = branchName + "_BitmapTexture"
	gammaGain.name = branchName + "_GammaGain"

	return gammaGain
)

fn setEnvironment createBackground createLighting createReflection backgroundFilePath lightingFilePath reflectionFilePath =
(
	local rayswitchShader = if(not hasProductionShadersSupport()) then
		mr_Raytype_Switcher__advanced()
	else
		Color_Override_Ray_Type_Switcher()
	environmentMap = rayswitchShader

	if(createBackground == 1) then
	(
		local backgroundBranch = getEnvironmentBranch (Constants.prefix + "_Background") backgroundFilePath
		rayswitchShader.eye = backgroundBranch
	)

	if(createLighting == 1) then
	(
		local lightingBranch = getEnvironmentBranch (Constants.prefix + "_Lighting") lightingFilePath
		rayswitchShader.finalgather = lightingBranch
	)

	if(createReflection == 1) then
	(
		local reflectionBranch = getEnvironmentBranch (Constants.prefix + "_Reflection") reflectionFilePath
		rayswitchShader.environment = reflectionBranch
		rayswitchShader.reflection = reflectionBranch
		rayswitchShader.refraction = reflectionBranch
	)

	rayswitchShader.name = (Constants.prefix + "_RaySwitch")
)

fn connectFeedbackToSIBLShaders createBackground createLighting createReflection =
(
	if(createBackground == 1) then
	(
		paramWire.connect SceneNodes.feedback.rotation.controller[#Z_Rotation] environmentMap.eye.input_shader.coordinates[#U_Offset] "(Z_Rotation-(pi*2))*2/pi/4"
	)

	if(createLighting == 1) then
	(
		paramWire.connect SceneNodes.feedback.rotation.controller[#Z_Rotation] environmentMap.finalgather.input_shader.coordinates[#U_Offset] "(Z_Rotation-(pi*2))*2/pi/4"
	)

	if(createReflection == 1) then
	(
		paramWire.connect SceneNodes.feedback.rotation.controller[#Z_Rotation] environmentMap.environment.input_shader.coordinates[#U_Offset] "(Z_Rotation-(pi*2))*2/pi/4"
		paramWire.connect SceneNodes.feedback.rotation.controller[#Z_Rotation] environmentMap.reflection.input_shader.coordinates[#U_Offset] "(Z_Rotation-(pi*2))*2/pi/4"
		paramWire.connect SceneNodes.feedback.rotation.controller[#Z_Rotation] environmentMap.refraction.input_shader.coordinates[#U_Offset] "(Z_Rotation-(pi*2))*2/pi/4"
	)
)

fn connectFeedbackToLights =
(
	if(SceneNodes.lightsGroup != undefined) then
	(
		paramWire.connect SceneNodes.feedback.rotation.controller[#Z_Rotation] SceneNodes.lightsGroup.rotation.controller[#Z_Rotation] "Z_Rotation"
		paramWire.connect SceneNodes.feedback.transform.controller[#Scale] SceneNodes.lightsGroup.transform.controller[#Scale] "Scale"
	)
)

fn createGround currentScale shadowCatcher =
(
	local currentScale = currentScale * (sqrt 2)
	local currentPlane = plane typeinCreationMethod:1 length:currentScale width:currentScale
	currentPlane.backfacecull = on
	currentPlane.parent = SceneNodes.dataGroup

	local groundShader = if(shadowCatcher == 1) then
		Matte_Shadow_Reflection__mi background_shader:environmentMap.eye ambient_intensity:1.0 ambient:(color 20 20 20) ao_samples:32 ao_distance:0.0 ao_dark:(color 40 40 40)
	else
		StandardMaterial name:(Constants.prefix + "_GroundShader")
	currentPlane.material = groundShader

	currentPlane.name = (Constants.prefix + "_Ground")
)

fn activateLinearWorkflow createBackground =
(
	local lensShader = Utility_Gamma___Gain__mi()
	renderers.current.Camera_Lens_Shader = lensShader
	lensShader.reverse = False
	lensShader.name = (Constants.prefix + "_Lens")

	if(createBackground == 1) then
		environmentMap.eye.gamma = Constants.defaultGamma
)

fn setup =
(
	if(not hasProductionShadersSupport()) then
		try
		(
			local rayswitchShader = mr_Raytype_Switcher__advanced()
			rayswitchShader = undefined
		)
		catch
		(
			messageBox (Constants.applicationName + " | Mental Ray Productions Shaders are not available!") title:(Constants.applicationName + " Error")
			return ()
		)

	if(mental_ray_renderer != undefined) then
	(
		if(execute(Constants.globalPreProcessCallback) != undefined) then
		(
			print (Constants.applicationName + " | Executing overall preprocess Callback!")
			execute(Constants.globalPreProcessCallback + "()")
		)

		if(execute(Constants.localPreProcessCommand) != undefined) then
		(
			print (Constants.applicationName + " | Executing Template specific preprocess Callback!")
			execute(Constants.localPreProcessCommand + "()")
		)

		print (Constants.applicationName + " | Starting '" +  Constants.title + "' file import!")

		environmentDeletion()

		UserOptions.feedbackRadius = getExtendedFeedbackRadius UserOptions.feedbackRadius

		activateRenderer()

		if(UserOptions.hideLights == 1) then
			hideLights()

		getControlsGroup()

		if(UserOptions.displayFeedback == 1) then
			getFeedback UserOptions.feedbackRadius  UserOptions.createBackground Datas.backgroundFilePath

		createSkyLight()

		setEnvironment UserOptions.createBackground UserOptions.createLighting UserOptions.createReflection Datas.backgroundFilePath Datas.lightingFilePath Datas.reflectionFilePath

		if(UserOptions.displayFeedback == 1) then
			connectFeedbackToSIBLShaders  UserOptions.createBackground UserOptions.createLighting UserOptions.createReflection

		if(UserOptions.createSun == 1 and Datas.sunU != -1 and Datas.sunV != -1) then
			createSun UserOptions.sunLightType Datas.sunColor Datas.sunMultiplier UserOptions.feedbackRadius Datas.sunU Datas.sunV

		if(UserOptions.createLights == 1 and Datas.dynamicLights != "-1") then
			createDynamicLights UserOptions.dynamicLightsType Datas.dynamicLights UserOptions.feedbackRadius

		if(UserOptions.displayFeedback == 1) then
			connectFeedbackToLights()

		if(UserOptions.createGround == 1) then
			createGround UserOptions.feedbackRadius UserOptions.shadowCatcher

		if(UserOptions.activateLinearWorkflow == 1) then
			activateLinearWorkflow UserOptions.createBackground

		print (Constants.applicationName + " | '" +  Constants.title + "' file import finished!")

		if(execute(Constants.globalPostProcessCallback) != undefined) then
		(
			print (Constants.applicationName + " | Executing overall postprocess Callback!")
			execute(Constants.globalPostProcessCallback + "()")
		)

		if(execute(Constants.localPostProcessCommand) != undefined) then
		(
			print (Constants.applicationName + " | Executing Template specific postprocess Callback!")
			execute(Constants.localPostProcessCommand + "()")
		)
	)
	else
		messageBox (Constants.applicationName + " | Mental Ray renderer is not available!") title:(Constants.applicationName + " Error")
)

(
	local proceedSetup = 1
	if(Datas.backgroundFilePath == "-1" or Datas.reflectionFilePath == "-1" or Datas.lightingFilePath == "-1") then
	(
		if not queryBox (Constants.applicationName + " | Non Template compatible Ibl Set provided: Unpredictable results may occur! Would you like to proceed anyway?") beep:false then
			proceedSetup = 0
	)

	if(proceedSetup == 1) then
		setup()
	else
		print (Constants.applicationName + " | '" +  Constants.title + "' file import aborted!")
)
